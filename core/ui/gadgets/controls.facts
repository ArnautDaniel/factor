IN: gadgets
USING: help gadgets-buttons gadgets-panes gadgets-labels
gadgets-sliders generic models kernel ;

HELP: control
{ $class-description "A control is a " { $link gadget } " linked to a " { $link model } " stored in the " { $link control-model } " slot. Changes to the model are reflected in the appearance and behavior of the control, and the control may in turn change the value of the model in response to user input."
$nl
"Controls are created by calling " { $link <control> } " and " { $link delegate>control } ". Other words which create controls include:"
{ $list
    { $link <label-control> }
    { $link <radio-control> }
    { $link <pane-control> }
}
$nl
"Objects may delegate to " { $link control } " instances, in which case the " { $link control-self } " slot must be set to the frontmost object in the delegation chain. This ensures that the correct object receives notification of model changes." } ;

HELP: <control>
{ $values { "model" model } { "gadget" gadget } { "quot" "a quotation with stack effect " { $snippet "( value control -- )" } } }
{ $description "Creates a new control linked to the given model. The gadget parameter becomes the control's delegate. The quotation is called when the model value changes," }
{ $examples
    "The following example creates a gadget whose fill color is determined by the value of a model:"
    { $code
        "USING: gadgets gadgets-panes models ;"
        ": set-fill-color >r <solid> r> set-gadget-interior ;"
        "{ 1.0 0.0 0.5 1.0 } <model>"
        "<gadget> [ set-fill-color ] <control>"
        "{ 100 100 } over set-rect-dim"
        "gadget."
    }
    "The " { $module-link "demos/color-picker" } " module extends this example into an elaborate color chooser where " { $link slider } " gadgets modify the red/green/blue components of a gadget's fill color."
} ;

{ <control> delegate>control control-value set-control-value } related-words

HELP: control-value
{ $values { "control" control } { "value" object } }
{ $description "Outputs the value of the control's model." } ;

HELP: set-control-value
{ $values { "value" object } { "control" control } }
{ $description "Sets the value of the control's model." } ;

HELP: delegate>control
{ $values { "tuple" tuple } { "model" model } { "underlying" gadget } }
{ $description "Creates a new " { $link control } " and creates a delegation chain where " { $snippet "tuple" } " at the front delegates to the new " { $link control } " which delegates to " { $snippet "underlying" } ". The " { $link model-changed } " word is called on " { $snippet "tuple" } " when the model changes." }
{ $side-effects "tuple" } ;
