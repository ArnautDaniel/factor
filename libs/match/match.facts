! Copyright (C) 2006 Chris Double.
! See http://factorcode.org/license.txt for BSD license.
USING: help match namespaces ;

HELP: match 
{ $values { "value1" "an object" } { "value2" "an object" } { "bindings" "A hashtable" }
}
{ $description "Pattern match value1 against value2. These values can be any Factor value, including sequences and tuples. The values can contain pattern variables, which are symbols that begin with '?'. The result is a hashtable of the bindings, mapping the pattern variables from one sequence to the equivalent value in the other sequence. The '_' symbol can be used to ignore the value at that point in the pattern for the match. " } 
{ $examples 
    { $example "USE: match" "MATCH-VARS: ?a ?b ;\n{ ?a { 2 ?b } 5 } { 1 { 2 3 } _ } match ." "H{ { ?a 1 } { ?b 3 } }" }
}
{ $see-also match-cond POSTPONE: MATCH-VARS: replace-patterns match-replace } ;

HELP: match-cond
{ $values { "value" "an object" } { "assoc" "A sequence of quotation pairs" } }
{ $description "Calls the second quotation in the first pair whose first sequence yields a successful " { $link match } " against 'value'. The second quotation, when called, has the hashtable returned from the " { $link match } " call bound as the top namespace so " { $link get } " can be used to retrieve the values. To have a fallthrough match clause use the '_' match variable." } 
{ $examples 
    { $code "USE: match" "MATCH-VARS: ?value ;\n{ increment ?value } {\n  { { increment ?value } [ ?value do-something ] }\n  { { decrement ?value } [ ?value do-something-else ] }\n  { _ [ no-match-found ] }\n} match-cond" }
}
{ $see-also match POSTPONE: MATCH-VARS: replace-patterns match-replace } ;


HELP: MATCH-VARS:
{ $syntax "MATCH-VARS: var ... ;" }
{ $values { "var" "a match variable name beginning with '?'" } }
{ $description "Creates a symbol that can be used in " { $link match } " and " { $link match-cond } " for binding values in the matched sequence. The symbol name is created as a word that is defined to get the value of the symbol out of the current namespace. This can be used in " { $link match-cond } " to retrive the values in the quotation body." }
{ $examples 
    { $code "USE: match" "MATCH-VARS: ?value ;\n{ increment ?value } {\n  { { increment ?value } [ ?value do-something ] }\n  { { decrement ?value } [ ?value do-something-else ] }\n  { _ [ no-match-found ] }\n} match-cond" }
}
{ $see-also match match-cond replace-patterns match-replace } ;

HELP: replace-patterns
{ $values { "object" "an object" } { "result" "an object" } }
{ $description "Copy the object, replacing each occurrence of a pattern matching variable with the actual value of that variable." } 
{ $examples
  { $example 
      "USE: match" "MATCH-VARS: ?a ?b ?c ;" "{ ?a ?b ?c } { 1 2 3 } match [\n  { ?a { ?b ?c } } replace-patterns\n] bind ." 
      "{ 1 { 2 3 } }"
  }
  { $example 
      "USE: match" "MATCH-VARS: ?a ?b ;" "TUPLE: foo a b ;\nT{ foo f ?a ?b } 1 2 <foo> match [\n  T{ foo f ?b ?a } replace-patterns\n] bind ." 
      "T{ foo f 2 1 }"
  }
}
{ $see-also match-cond POSTPONE: MATCH-VARS: match-replace } ;

HELP: match-replace
{ $values { "object" "an object" } { "pattern1" "an object" } { "pattern2" "an object" } { "result" "an object" } }
{ $description "Matches the 'object' against 'pattern1'. The pattern match variables in 'pattern1' are assigned the values from the matching 'object'. These are then replaced into the 'pattern2' pattern match variables." } 
{ $examples
  { $example 
      "USE: match" "{ 1 2 } { ?a ?b } { ?b ?a } match-replace ." 
      "{ 2 1 }"
  }
  { $example 
      "USE: match" "TUPLE: foo a b ;\n1 2 <foo> T{ foo f ?a ?b } T{ foo f ?b ?a } match-replace ." 
      "T{ foo f 2 1 }"
  }
}
{ $see-also match-cond POSTPONE: MATCH-VARS: } ;
